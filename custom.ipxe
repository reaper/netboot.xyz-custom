#!ipxe
goto ${menu} ||

:live_menu
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i686 ||
set os Clonezilla
menu ${os}
item --gap ${os} Versions
item debian ${space} ${os} Debian Based
iseq ${os_arch} amd64 && item ubuntu ${space} ${os} Ubuntu Based ||
choose live_version || goto live_exit
menu ${os} ${live_version} - ${os_arch}
item --gap ${os} Flavors
goto ${live_version}

:debian
iseq ${os_arch} i686 && item /debian-squash/releases/download/2.8.0-27-21f7bd02/ ${space} Clonezilla 2.8.0-27 Stable ||
iseq ${os_arch} amd64 && item /debian-squash/releases/download/2.8.0-27-a90fc92f/ ${space} Clonezilla 2.8.0-27 Stable ||
iseq ${os_arch} amd64 && item /debian-squash/releases/download/2.8.1-10-5fb1a15b/ ${space} Clonezilla 2.8.1-10 Testing ||
iseq ${os_arch} i686 && item /debian-squash/releases/download/2.8.1-10-ee3863df/ ${space} Clonezilla 2.8.1-10 Testing ||
choose path || goto live_menu
goto clonezilla-boot

:ubuntu
iseq ${os_arch} amd64 && item /ubuntu-squash/releases/download/20211116-impish-86411ada/ ${space} Clonezilla 20211116-Impish Stable ||
iseq ${os_arch} amd64 && item /ubuntu-squash/releases/download/20211221-impish-5774fbed/ ${space} Clonezilla 20211221-Impish Testing ||
choose path || goto live_menu
goto clonezilla-boot

:clonezilla-boot
imgfree
set url ${live_endpoint}${path}
kernel ${url}vmlinuz boot=live username=user union=overlay config components noswap edd=on nomodeset locales=en_US.UTF-8 keyboard-layouts=fr ocs_prerun1="dhclient -v eth0" ocs_prerun2="sleep 2" ocs_prerun3="mount -t cifs -o vers=1.0,rw,sec=ntlm,username=reaper,password=reaper //10.11.0.13/CAPSULE/Backups/Abella /home/partimag" ocs_prerun4="sleep 2" ocs_live_run="ocs-sr -p poweroff -z1 -noabo -senc savedisk autoname sda" ocs_live_batch=no net.ifnames=0 nosplash noprompt fetch=${url}filesystem.squashfs initrd=initrd.magic ${cmdline}
initrd ${url}initrd
boot

:live_exit
clear menu
exit 0
